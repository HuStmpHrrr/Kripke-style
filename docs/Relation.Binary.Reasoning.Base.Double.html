<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Relation.Binary.Reasoning.Base.Double</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- The basic code for equational reasoning with two relations:</a>
<a class="Comment" id="169">-- equality and some other ordering.</a>
<a class="Comment" id="206">------------------------------------------------------------------------</a>
<a class="Comment" id="279">--</a>
<a class="Comment" id="282">-- See `Data.Nat.Properties` or `Relation.Binary.Reasoning.PartialOrder`</a>
<a class="Comment" id="355">-- for examples of how to instantiate this module.</a>

<a class="Symbol" id="407">{-#</a> <a class="Keyword" id="411">OPTIONS</a> <a class="Pragma" id="419">--without-K</a> <a class="Pragma" id="431">--safe</a> <a class="Symbol" id="438">#-}</a>

<a class="Keyword" id="443">open</a> <a class="Keyword" id="448">import</a> <a class="Module" href="Relation.Binary.html" id="455">Relation.Binary</a>

<a class="Keyword" id="472">module</a> <a class="Module" href="Relation.Binary.Reasoning.Base.Double.html" id="479">Relation.Binary.Reasoning.Base.Double</a> <a class="Symbol" id="517">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#518" id="518">a</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#520" id="520">ℓ₁</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#523" id="523">ℓ₂</a><a class="Symbol" id="525">}</a> <a class="Symbol" id="527">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="528">A</a> <a class="Symbol" id="530">:</a> <a class="Primitive" href="Agda.Primitive.html#326" id="532">Set</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#518" id="536">a</a><a class="Symbol" id="537">}</a>
  <a class="Symbol" id="541">{</a><a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="542">_≈_</a> <a class="Symbol" id="546">:</a> <a class="Function" href="Relation.Binary.Core.html#882" id="548">Rel</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="552">A</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#520" id="554">ℓ₁</a><a class="Symbol" id="556">}</a> <a class="Symbol" id="558">{</a><a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#559" id="559">_∼_</a> <a class="Symbol" id="563">:</a> <a class="Function" href="Relation.Binary.Core.html#882" id="565">Rel</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="569">A</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#523" id="571">ℓ₂</a><a class="Symbol" id="573">}</a> <a class="Symbol" id="575">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#576" id="576">isPreorder</a> <a class="Symbol" id="587">:</a> <a class="Record" href="Relation.Binary.Structures.html#2163" id="589">IsPreorder</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="600">_≈_</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#559" id="604">_∼_</a><a class="Symbol" id="607">)</a>
  <a class="Keyword" id="611">where</a>

<a class="Keyword" id="618">open</a> <a class="Keyword" id="623">import</a> <a class="Module" href="Data.Product.html" id="630">Data.Product</a> <a class="Keyword" id="643">using</a> <a class="Symbol" id="649">(</a><a class="Field" href="Agda.Builtin.Sigma.html#252" id="650">proj₁</a><a class="Symbol" id="655">;</a> <a class="Field" href="Agda.Builtin.Sigma.html#264" id="657">proj₂</a><a class="Symbol" id="662">)</a>
<a class="Keyword" id="664">open</a> <a class="Keyword" id="669">import</a> <a class="Module" href="Level.html" id="676">Level</a> <a class="Keyword" id="682">using</a> <a class="Symbol" id="688">(</a><a class="Postulate" href="Agda.Primitive.html#597" id="689">Level</a><a class="Symbol" id="694">;</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="696">_⊔_</a><a class="Symbol" id="699">;</a> <a class="Record" href="Level.html#400" id="701">Lift</a><a class="Symbol" id="705">;</a> <a class="InductiveConstructor" href="Level.html#457" id="707">lift</a><a class="Symbol" id="711">)</a>
<a class="Keyword" id="713">open</a> <a class="Keyword" id="718">import</a> <a class="Module" href="Function.Base.html" id="725">Function.Base</a> <a class="Keyword" id="739">using</a> <a class="Symbol" id="745">(</a><a class="Function Operator" href="Function.Base.html#3973" id="746">case_of_</a><a class="Symbol" id="754">;</a> <a class="Function" href="Function.Base.html#695" id="756">id</a><a class="Symbol" id="758">)</a>
<a class="Keyword" id="760">open</a> <a class="Keyword" id="765">import</a> <a class="Module" href="Relation.Binary.PropositionalEquality.Core.html" id="772">Relation.Binary.PropositionalEquality.Core</a>
  <a class="Keyword" id="817">using</a> <a class="Symbol" id="823">(</a><a class="Datatype Operator" href="Agda.Builtin.Equality.html#151" id="824">_≡_</a><a class="Symbol" id="827">;</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#208" id="829">refl</a><a class="Symbol" id="833">;</a> <a class="Function" href="Relation.Binary.PropositionalEquality.Core.html#1684" id="835">sym</a><a class="Symbol" id="838">)</a>
<a class="Keyword" id="840">open</a> <a class="Keyword" id="845">import</a> <a class="Module" href="Relation.Nullary.html" id="852">Relation.Nullary</a> <a class="Keyword" id="869">using</a> <a class="Symbol" id="875">(</a><a class="Record" href="Relation.Nullary.html#1511" id="876">Dec</a><a class="Symbol" id="879">;</a> <a class="InductiveConstructor" href="Relation.Nullary.html#1648" id="881">yes</a><a class="Symbol" id="884">;</a> <a class="InductiveConstructor" href="Relation.Nullary.html#1685" id="886">no</a><a class="Symbol" id="888">)</a>
<a class="Keyword" id="890">open</a> <a class="Keyword" id="895">import</a> <a class="Module" href="Relation.Nullary.Decidable.html" id="902">Relation.Nullary.Decidable</a> <a class="Keyword" id="929">using</a> <a class="Symbol" id="935">(</a><a class="Function" href="Relation.Nullary.Decidable.Core.html#1365" id="936">True</a><a class="Symbol" id="940">;</a> <a class="Function" href="Relation.Nullary.Decidable.Core.html#1484" id="942">toWitness</a><a class="Symbol" id="951">)</a>

<a class="Keyword" id="954">open</a> <a class="Module" href="Relation.Binary.Structures.html#2163" id="959">IsPreorder</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#576" id="970">isPreorder</a>

<a class="Comment" id="982">------------------------------------------------------------------------</a>
<a class="Comment" id="1055">-- A datatype to hide the current relation type</a>

<a class="Keyword" id="1104">infix</a> <a class="Number" id="1110">4</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1112">_IsRelatedTo_</a>

<a class="Keyword" id="1127">data</a> <a id="_IsRelatedTo_"></a><a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1132">_IsRelatedTo_</a> <a class="Symbol" id="1146">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1147" id="1147">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1149" id="1149">y</a> <a class="Symbol" id="1151">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="1153">A</a><a class="Symbol" id="1154">)</a> <a class="Symbol" id="1156">:</a> <a class="Primitive" href="Agda.Primitive.html#326" id="1158">Set</a> <a class="Symbol" id="1162">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#518" id="1163">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="1165">⊔</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#520" id="1167">ℓ₁</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="1170">⊔</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#523" id="1172">ℓ₂</a><a class="Symbol" id="1174">)</a> <a class="Keyword" id="1176">where</a>
  <a id="_IsRelatedTo_.nonstrict"></a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="1184">nonstrict</a> <a class="Symbol" id="1194">:</a> <a class="Symbol" id="1196">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1197" id="1197">x∼y</a> <a class="Symbol" id="1201">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1147" id="1203">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#559" id="1205">∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1149" id="1207">y</a><a class="Symbol" id="1208">)</a> <a class="Symbol" id="1210">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1147" id="1212">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1214">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1149" id="1226">y</a>
  <a id="_IsRelatedTo_.equals"></a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="1230">equals</a>    <a class="Symbol" id="1240">:</a> <a class="Symbol" id="1242">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1243" id="1243">x≈y</a> <a class="Symbol" id="1247">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1147" id="1249">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="1251">≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1149" id="1253">y</a><a class="Symbol" id="1254">)</a> <a class="Symbol" id="1256">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1147" id="1258">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1260">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1149" id="1272">y</a>

<a class="Comment" id="1275">------------------------------------------------------------------------</a>
<a class="Comment" id="1348">-- A record that is used to ensure that the final relation proved by the</a>
<a class="Comment" id="1421">-- chain of reasoning can be converted into the required relation.</a>

<a class="Keyword" id="1489">data</a> <a id="IsEquality"></a><a class="Datatype" href="Relation.Binary.Reasoning.Base.Double.html#1494" id="1494">IsEquality</a> <a class="Symbol" id="1505">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1506" id="1506">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1508" id="1508">y</a><a class="Symbol" id="1509">}</a> <a class="Symbol" id="1511">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1506" id="1513">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1515">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1508" id="1527">y</a> <a class="Symbol" id="1529">→</a> <a class="Primitive" href="Agda.Primitive.html#326" id="1531">Set</a> <a class="Symbol" id="1535">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#518" id="1536">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="1538">⊔</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#520" id="1540">ℓ₁</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="1543">⊔</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#523" id="1545">ℓ₂</a><a class="Symbol" id="1547">)</a> <a class="Keyword" id="1549">where</a>
  <a id="IsEquality.isEquality"></a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1557" id="1557">isEquality</a> <a class="Symbol" id="1568">:</a> <a class="Symbol" id="1570">∀</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1572" id="1572">x≈y</a> <a class="Symbol" id="1576">→</a> <a class="Datatype" href="Relation.Binary.Reasoning.Base.Double.html#1494" id="1578">IsEquality</a> <a class="Symbol" id="1589">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="1590">equals</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1572" id="1597">x≈y</a><a class="Symbol" id="1600">)</a>

<a id="IsEquality?"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1603" id="1603">IsEquality?</a> <a class="Symbol" id="1615">:</a> <a class="Symbol" id="1617">∀</a> <a class="Symbol" id="1619">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1620" id="1620">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1622" id="1622">y</a><a class="Symbol" id="1623">}</a> <a class="Symbol" id="1625">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1626" id="1626">x≲y</a> <a class="Symbol" id="1630">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1620" id="1632">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1634">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1622" id="1646">y</a><a class="Symbol" id="1647">)</a> <a class="Symbol" id="1649">→</a> <a class="Record" href="Relation.Nullary.html#1511" id="1651">Dec</a> <a class="Symbol" id="1655">(</a><a class="Datatype" href="Relation.Binary.Reasoning.Base.Double.html#1494" id="1656">IsEquality</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1626" id="1667">x≲y</a><a class="Symbol" id="1670">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1603" id="1672">IsEquality?</a> <a class="Symbol" id="1684">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="1685">nonstrict</a> <a class="Symbol" id="1695">_)</a> <a class="Symbol" id="1698">=</a> <a class="InductiveConstructor" href="Relation.Nullary.html#1685" id="1700">no</a> <a class="Symbol" id="1703">λ()</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1603" id="1707">IsEquality?</a> <a class="Symbol" id="1719">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="1720">equals</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1727" id="1727">x≈y</a><a class="Symbol" id="1730">)</a>  <a class="Symbol" id="1733">=</a> <a class="InductiveConstructor" href="Relation.Nullary.html#1648" id="1735">yes</a> <a class="Symbol" id="1739">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1557" id="1740">isEquality</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1727" id="1751">x≈y</a><a class="Symbol" id="1754">)</a>

<a id="extractEquality"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1757" id="1757">extractEquality</a> <a class="Symbol" id="1773">:</a> <a class="Symbol" id="1775">∀</a> <a class="Symbol" id="1777">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1778" id="1778">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1780" id="1780">y</a><a class="Symbol" id="1781">}</a> <a class="Symbol" id="1783">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1784" id="1784">x≲y</a> <a class="Symbol" id="1788">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1778" id="1790">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="1792">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1780" id="1804">y</a><a class="Symbol" id="1805">}</a> <a class="Symbol" id="1807">→</a> <a class="Datatype" href="Relation.Binary.Reasoning.Base.Double.html#1494" id="1809">IsEquality</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1784" id="1820">x≲y</a> <a class="Symbol" id="1824">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1778" id="1826">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="1828">≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1780" id="1830">y</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1757" id="1832">extractEquality</a> <a class="Symbol" id="1848">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1557" id="1849">isEquality</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1860" id="1860">x≈y</a><a class="Symbol" id="1863">)</a> <a class="Symbol" id="1865">=</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#1860" id="1867">x≈y</a>

<a class="Comment" id="1872">------------------------------------------------------------------------</a>
<a class="Comment" id="1945">-- Reasoning combinators</a>

<a class="Comment" id="1971">-- See `Relation.Binary.Reasoning.Base.Partial` for the design decisions</a>
<a class="Comment" id="2044">-- behind these combinators.</a>

<a class="Keyword" id="2074">infix</a>  <a class="Number" id="2081">1</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2213" id="2083">begin_</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2327" id="2090">begin-equality_</a>
<a class="Keyword" id="2106">infixr</a> <a class="Number" id="2113">2</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2499" id="2115">step-∼</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2721" id="2122">step-≈</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2963" id="2129">step-≈˘</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3123" id="2137">step-≡</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3398" id="2144">step-≡˘</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#3551" id="2152">_≡⟨⟩_</a>
<a class="Keyword" id="2158">infix</a>  <a class="Number" id="2165">3</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#3647" id="2167">_∎</a>

<a class="Comment" id="2171">-- Beginnings of various types of proofs</a>

<a id="begin_"></a><a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2213" id="2213">begin_</a> <a class="Symbol" id="2220">:</a> <a class="Symbol" id="2222">∀</a> <a class="Symbol" id="2224">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2225" id="2225">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2227" id="2227">y</a><a class="Symbol" id="2228">}</a> <a class="Symbol" id="2230">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2231" id="2231">r</a> <a class="Symbol" id="2233">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2225" id="2235">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2237">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2227" id="2249">y</a><a class="Symbol" id="2250">)</a> <a class="Symbol" id="2252">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2225" id="2254">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#559" id="2256">∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2227" id="2258">y</a>
<a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2213" id="2260">begin</a> <a class="Symbol" id="2266">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="2267">nonstrict</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2277" id="2277">x∼y</a><a class="Symbol" id="2280">)</a> <a class="Symbol" id="2282">=</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2277" id="2284">x∼y</a>
<a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2213" id="2288">begin</a> <a class="Symbol" id="2294">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="2295">equals</a>    <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2305" id="2305">x≈y</a><a class="Symbol" id="2308">)</a> <a class="Symbol" id="2310">=</a> <a class="Field" href="Relation.Binary.Structures.html#2331" id="2312">reflexive</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2305" id="2322">x≈y</a>

<a id="begin-equality_"></a><a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2327" id="2327">begin-equality_</a> <a class="Symbol" id="2343">:</a> <a class="Symbol" id="2345">∀</a> <a class="Symbol" id="2347">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2348" id="2348">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2350" id="2350">y</a><a class="Symbol" id="2351">}</a> <a class="Symbol" id="2353">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2354" id="2354">r</a> <a class="Symbol" id="2356">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2348" id="2358">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2360">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2350" id="2372">y</a><a class="Symbol" id="2373">)</a> <a class="Symbol" id="2375">→</a> <a class="Symbol" id="2377">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2378" id="2378">s</a> <a class="Symbol" id="2380">:</a> <a class="Function" href="Relation.Nullary.Decidable.Core.html#1365" id="2382">True</a> <a class="Symbol" id="2387">(</a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1603" id="2388">IsEquality?</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2354" id="2400">r</a><a class="Symbol" id="2401">)}</a> <a class="Symbol" id="2404">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2348" id="2406">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="2408">≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2350" id="2410">y</a>
<a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#2327" id="2412">begin-equality_</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2428" id="2428">r</a> <a class="Symbol" id="2430">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2431" id="2431">s</a><a class="Symbol" id="2432">}</a> <a class="Symbol" id="2434">=</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#1757" id="2436">extractEquality</a> <a class="Symbol" id="2452">(</a><a class="Function" href="Relation.Nullary.Decidable.Core.html#1484" id="2453">toWitness</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2431" id="2463">s</a><a class="Symbol" id="2464">)</a>

<a class="Comment" id="2467">-- Step with the main relation</a>

<a id="step-∼"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2499" id="2499">step-∼</a> <a class="Symbol" id="2506">:</a> <a class="Symbol" id="2508">∀</a> <a class="Symbol" id="2510">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2511" id="2511">x</a> <a class="Symbol" id="2513">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="2515">A</a><a class="Symbol" id="2516">)</a> <a class="Symbol" id="2518">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2519" id="2519">y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2521" id="2521">z</a><a class="Symbol" id="2522">}</a> <a class="Symbol" id="2524">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2519" id="2526">y</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2528">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2521" id="2540">z</a> <a class="Symbol" id="2542">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2511" id="2544">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#559" id="2546">∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2519" id="2548">y</a> <a class="Symbol" id="2550">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2511" id="2552">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2554">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2521" id="2566">z</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2499" id="2568">step-∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2575" id="2575">x</a> <a class="Symbol" id="2577">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="2578">nonstrict</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2588" id="2588">y∼z</a><a class="Symbol" id="2591">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2593" id="2593">x∼y</a> <a class="Symbol" id="2597">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="2599">nonstrict</a> <a class="Symbol" id="2609">(</a><a class="Field" href="Relation.Binary.Structures.html#2361" id="2610">trans</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2593" id="2616">x∼y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2588" id="2620">y∼z</a><a class="Symbol" id="2623">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2499" id="2625">step-∼</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2632" id="2632">x</a> <a class="Symbol" id="2634">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="2635">equals</a>    <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2645" id="2645">y≈z</a><a class="Symbol" id="2648">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2650" id="2650">x∼y</a> <a class="Symbol" id="2654">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="2656">nonstrict</a> <a class="Symbol" id="2666">(</a><a class="Function" href="Relation.Binary.Structures.html#2579" id="2667">∼-respʳ-≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2645" id="2677">y≈z</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2650" id="2681">x∼y</a><a class="Symbol" id="2684">)</a>

<a class="Comment" id="2687">-- Step with the setoid equality</a>

<a id="step-≈"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2721" id="2721">step-≈</a> <a class="Symbol" id="2728">:</a> <a class="Symbol" id="2730">∀</a> <a class="Symbol" id="2732">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2733" id="2733">x</a> <a class="Symbol" id="2735">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="2737">A</a><a class="Symbol" id="2738">)</a> <a class="Symbol" id="2740">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2741" id="2741">y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2743" id="2743">z</a><a class="Symbol" id="2744">}</a> <a class="Symbol" id="2746">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2741" id="2748">y</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2750">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2743" id="2762">z</a> <a class="Symbol" id="2764">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2733" id="2766">x</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="2768">≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2741" id="2770">y</a> <a class="Symbol" id="2772">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2733" id="2774">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2776">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2743" id="2788">z</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2721" id="2790">step-≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2797" id="2797">x</a> <a class="Symbol" id="2799">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="2800">nonstrict</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2810" id="2810">y∼z</a><a class="Symbol" id="2813">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2815" id="2815">x≈y</a> <a class="Symbol" id="2819">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="2821">nonstrict</a> <a class="Symbol" id="2831">(</a><a class="Function" href="Relation.Binary.Structures.html#2489" id="2832">∼-respˡ-≈</a> <a class="Symbol" id="2842">(</a><a class="Function" href="Relation.Binary.Structures.html#1594" id="2843">Eq.sym</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2815" id="2850">x≈y</a><a class="Symbol" id="2853">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2810" id="2855">y∼z</a><a class="Symbol" id="2858">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2721" id="2860">step-≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2867" id="2867">x</a> <a class="Symbol" id="2869">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="2870">equals</a>    <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2880" id="2880">y≈z</a><a class="Symbol" id="2883">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2885" id="2885">x≈y</a> <a class="Symbol" id="2889">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="2891">equals</a>    <a class="Symbol" id="2901">(</a><a class="Function" href="Relation.Binary.Structures.html#1620" id="2902">Eq.trans</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2885" id="2911">x≈y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2880" id="2915">y≈z</a><a class="Symbol" id="2918">)</a>

<a class="Comment" id="2921">-- Flipped step with the setoid equality</a>

<a id="step-≈˘"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2963" id="2963">step-≈˘</a> <a class="Symbol" id="2971">:</a> <a class="Symbol" id="2973">∀</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2975" id="2975">x</a> <a class="Symbol" id="2977">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2978" id="2978">y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2980" id="2980">z</a><a class="Symbol" id="2981">}</a> <a class="Symbol" id="2983">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2978" id="2985">y</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="2987">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2980" id="2999">z</a> <a class="Symbol" id="3001">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2978" id="3003">y</a> <a class="Bound Operator" href="Relation.Binary.Reasoning.Base.Double.html#542" id="3005">≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2975" id="3007">x</a> <a class="Symbol" id="3009">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2975" id="3011">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3013">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#2980" id="3025">z</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2963" id="3027">step-≈˘</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3035" id="3035">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3037" id="3037">y∼z</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3041" id="3041">x≈y</a> <a class="Symbol" id="3045">=</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2721" id="3047">step-≈</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3035" id="3054">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3037" id="3056">y∼z</a> <a class="Symbol" id="3060">(</a><a class="Function" href="Relation.Binary.Structures.html#1594" id="3061">Eq.sym</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3041" id="3068">x≈y</a><a class="Symbol" id="3071">)</a>

<a class="Comment" id="3074">-- Step with non-trivial propositional equality</a>

<a id="step-≡"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3123" id="3123">step-≡</a> <a class="Symbol" id="3130">:</a> <a class="Symbol" id="3132">∀</a> <a class="Symbol" id="3134">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3135" id="3135">x</a> <a class="Symbol" id="3137">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="3139">A</a><a class="Symbol" id="3140">)</a> <a class="Symbol" id="3142">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3143" id="3143">y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3145" id="3145">z</a><a class="Symbol" id="3146">}</a> <a class="Symbol" id="3148">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3143" id="3150">y</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3152">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3145" id="3164">z</a> <a class="Symbol" id="3166">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3135" id="3168">x</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#151" id="3170">≡</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3143" id="3172">y</a> <a class="Symbol" id="3174">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3135" id="3176">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3178">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3145" id="3190">z</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3123" id="3192">step-≡</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3199" id="3199">x</a> <a class="Symbol" id="3201">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="3202">nonstrict</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3212" id="3212">y∼z</a><a class="Symbol" id="3215">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3217" id="3217">x≡y</a> <a class="Symbol" id="3221">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1184" id="3223">nonstrict</a> <a class="Symbol" id="3233">(</a><a class="Function Operator" href="Function.Base.html#3973" id="3234">case</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3217" id="3239">x≡y</a> <a class="Function Operator" href="Function.Base.html#3973" id="3243">of</a> <a class="Symbol" id="3246">λ</a> <a class="Keyword" id="3248">where</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#208" id="3254">refl</a> <a class="Symbol" id="3259">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3212" id="3261">y∼z</a><a class="Symbol" id="3264">)</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3123" id="3266">step-≡</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3273" id="3273">x</a> <a class="Symbol" id="3275">(</a><a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="3276">equals</a>    <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3286" id="3286">y≈z</a><a class="Symbol" id="3289">)</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3291" id="3291">x≡y</a> <a class="Symbol" id="3295">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="3297">equals</a>    <a class="Symbol" id="3307">(</a><a class="Function Operator" href="Function.Base.html#3973" id="3308">case</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3291" id="3313">x≡y</a> <a class="Function Operator" href="Function.Base.html#3973" id="3317">of</a> <a class="Symbol" id="3320">λ</a> <a class="Keyword" id="3322">where</a> <a class="InductiveConstructor" href="Agda.Builtin.Equality.html#208" id="3328">refl</a> <a class="Symbol" id="3333">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3286" id="3335">y≈z</a><a class="Symbol" id="3338">)</a>

<a class="Comment" id="3341">-- Flipped step with non-trivial propositional equality</a>

<a id="step-≡˘"></a><a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3398" id="3398">step-≡˘</a> <a class="Symbol" id="3406">:</a> <a class="Symbol" id="3408">∀</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3410" id="3410">x</a> <a class="Symbol" id="3412">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3413" id="3413">y</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3415" id="3415">z</a><a class="Symbol" id="3416">}</a> <a class="Symbol" id="3418">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3413" id="3420">y</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3422">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3415" id="3434">z</a> <a class="Symbol" id="3436">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3413" id="3438">y</a> <a class="Datatype Operator" href="Agda.Builtin.Equality.html#151" id="3440">≡</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3410" id="3442">x</a> <a class="Symbol" id="3444">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3410" id="3446">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3448">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3415" id="3460">z</a>
<a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3398" id="3462">step-≡˘</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3470" id="3470">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3472" id="3472">y∼z</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3476" id="3476">x≡y</a> <a class="Symbol" id="3480">=</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3123" id="3482">step-≡</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3470" id="3489">x</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3472" id="3491">y∼z</a> <a class="Symbol" id="3495">(</a><a class="Function" href="Relation.Binary.PropositionalEquality.Core.html#1684" id="3496">sym</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3476" id="3500">x≡y</a><a class="Symbol" id="3503">)</a>

<a class="Comment" id="3506">-- Step with trivial propositional equality</a>

<a id="_≡⟨⟩_"></a><a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#3551" id="3551">_≡⟨⟩_</a> <a class="Symbol" id="3557">:</a> <a class="Symbol" id="3559">∀</a> <a class="Symbol" id="3561">(</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3562" id="3562">x</a> <a class="Symbol" id="3564">:</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#528" id="3566">A</a><a class="Symbol" id="3567">)</a> <a class="Symbol" id="3569">{</a><a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3570" id="3570">y</a><a class="Symbol" id="3571">}</a> <a class="Symbol" id="3573">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3562" id="3575">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3577">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3570" id="3589">y</a> <a class="Symbol" id="3591">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3562" id="3593">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3595">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3570" id="3607">y</a>
<a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3609" id="3609">x</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#3551" id="3611">≡⟨⟩</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3615" id="3615">x≲y</a> <a class="Symbol" id="3619">=</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3615" id="3621">x≲y</a>

<a class="Comment" id="3626">-- Termination step</a>

<a id="_∎"></a><a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#3647" id="3647">_∎</a> <a class="Symbol" id="3650">:</a> <a class="Symbol" id="3652">∀</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3654" id="3654">x</a> <a class="Symbol" id="3656">→</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3654" id="3658">x</a> <a class="Datatype Operator" href="Relation.Binary.Reasoning.Base.Double.html#1132" id="3660">IsRelatedTo</a> <a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3654" id="3672">x</a>
<a class="Bound" href="Relation.Binary.Reasoning.Base.Double.html#3674" id="3674">x</a> <a class="Function Operator" href="Relation.Binary.Reasoning.Base.Double.html#3647" id="3676">∎</a> <a class="Symbol" id="3678">=</a> <a class="InductiveConstructor" href="Relation.Binary.Reasoning.Base.Double.html#1230" id="3680">equals</a> <a class="Function" href="Relation.Binary.Structures.html#1568" id="3687">Eq.refl</a>

<a class="Comment" id="3696">-- Syntax declarations</a>

<a class="Keyword" id="3720">syntax</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2499" id="3727">step-∼</a>  <a class="Bound" id="3735">x</a> <a class="Bound" id="3737">y∼z</a> <a class="Bound" id="3741">x∼y</a> <a class="Symbol" id="3745">=</a> <a class="Bound" id="3747">x</a> <a class="Function" id="3749">∼⟨</a>  <a class="Bound" id="3753">x∼y</a> <a class="Function" id="3757">⟩</a> <a class="Bound" id="3759">y∼z</a>
<a class="Keyword" id="3763">syntax</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2721" id="3770">step-≈</a>  <a class="Bound" id="3778">x</a> <a class="Bound" id="3780">y∼z</a> <a class="Bound" id="3784">x≈y</a> <a class="Symbol" id="3788">=</a> <a class="Bound" id="3790">x</a> <a class="Function" id="3792">≈⟨</a>  <a class="Bound" id="3796">x≈y</a> <a class="Function" id="3800">⟩</a> <a class="Bound" id="3802">y∼z</a>
<a class="Keyword" id="3806">syntax</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#2963" id="3813">step-≈˘</a> <a class="Bound" id="3821">x</a> <a class="Bound" id="3823">y∼z</a> <a class="Bound" id="3827">y≈x</a> <a class="Symbol" id="3831">=</a> <a class="Bound" id="3833">x</a> <a class="Function" id="3835">≈˘⟨</a> <a class="Bound" id="3839">y≈x</a> <a class="Function" id="3843">⟩</a> <a class="Bound" id="3845">y∼z</a>
<a class="Keyword" id="3849">syntax</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3123" id="3856">step-≡</a>  <a class="Bound" id="3864">x</a> <a class="Bound" id="3866">y∼z</a> <a class="Bound" id="3870">x≡y</a> <a class="Symbol" id="3874">=</a> <a class="Bound" id="3876">x</a> <a class="Function" id="3878">≡⟨</a>  <a class="Bound" id="3882">x≡y</a> <a class="Function" id="3886">⟩</a> <a class="Bound" id="3888">y∼z</a>
<a class="Keyword" id="3892">syntax</a> <a class="Function" href="Relation.Binary.Reasoning.Base.Double.html#3398" id="3899">step-≡˘</a> <a class="Bound" id="3907">x</a> <a class="Bound" id="3909">y∼z</a> <a class="Bound" id="3913">y≡x</a> <a class="Symbol" id="3917">=</a> <a class="Bound" id="3919">x</a> <a class="Function" id="3921">≡˘⟨</a> <a class="Bound" id="3925">y≡x</a> <a class="Function" id="3929">⟩</a> <a class="Bound" id="3931">y∼z</a>
</pre></div></body></html>