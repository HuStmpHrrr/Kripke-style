<!DOCTYPE HTML>

<html><head><meta charset="utf-8"/><title>Algebra.Lattice.Structures.Biased</title><link href="Agda.css" rel="stylesheet"/><script src="highlight-hover.js" type="text/javascript"></script></head><body><div class="navbar"><a class="active" href="README.html">Everything</a><a href="index.html" style="float:right">Home</a></div><div class="main"><pre class="Agda"><a class="Comment" id="1">------------------------------------------------------------------------</a>
<a class="Comment" id="74">-- The Agda standard library</a>
<a class="Comment" id="103">--</a>
<a class="Comment" id="106">-- Some biased records for lattice-like structures. Such records are</a>
<a class="Comment" id="175">-- often easier to construct, but are suboptimal to use more widely and</a>
<a class="Comment" id="247">-- should be converted to the standard record definitions immediately</a>
<a class="Comment" id="317">-- using the provided conversion functions.</a>
<a class="Comment" id="361">------------------------------------------------------------------------</a>

<a class="Comment" id="435">-- The contents of this module should be accessed via `Algebra.Lattice`,</a>
<a class="Comment" id="508">-- unless you want to parameterise it via the equality relation.</a>

<a class="Symbol" id="574">{-#</a> <a class="Keyword" id="578">OPTIONS</a> <a class="Pragma" id="586">--without-K</a> <a class="Pragma" id="598">--safe</a> <a class="Symbol" id="605">#-}</a>

<a class="Keyword" id="610">open</a> <a class="Keyword" id="615">import</a> <a class="Module" href="Algebra.Core.html" id="622">Algebra.Core</a>
<a class="Keyword" id="635">open</a> <a class="Keyword" id="640">import</a> <a class="Module" href="Algebra.Consequences.Setoid.html" id="647">Algebra.Consequences.Setoid</a>
<a class="Keyword" id="675">open</a> <a class="Keyword" id="680">import</a> <a class="Module" href="Data.Product.html" id="687">Data.Product</a> <a class="Keyword" id="700">using</a> <a class="Symbol" id="706">(</a><a class="Field" href="Agda.Builtin.Sigma.html#252" id="707">proj₁</a><a class="Symbol" id="712">;</a> <a class="Field" href="Agda.Builtin.Sigma.html#264" id="714">proj₂</a><a class="Symbol" id="719">)</a>
<a class="Keyword" id="721">open</a> <a class="Keyword" id="726">import</a> <a class="Module" href="Level.html" id="733">Level</a> <a class="Keyword" id="739">using</a> <a class="Symbol" id="745">(</a><a class="Primitive Operator" href="Agda.Primitive.html#810" id="746">_⊔_</a><a class="Symbol" id="749">)</a>
<a class="Keyword" id="751">open</a> <a class="Keyword" id="756">import</a> <a class="Module" href="Relation.Binary.html" id="763">Relation.Binary</a> <a class="Keyword" id="779">using</a> <a class="Symbol" id="785">(</a><a class="Function" href="Relation.Binary.Core.html#882" id="786">Rel</a><a class="Symbol" id="789">;</a> <a class="Record" href="Relation.Binary.Bundles.html#1009" id="791">Setoid</a><a class="Symbol" id="797">;</a> <a class="Record" href="Relation.Binary.Structures.html#1522" id="799">IsEquivalence</a><a class="Symbol" id="812">)</a>

<a class="Keyword" id="815">module</a> <a class="Module" href="Algebra.Lattice.Structures.Biased.html" id="822">Algebra.Lattice.Structures.Biased</a>
  <a class="Symbol" id="858">{</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="859">a</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="861">ℓ</a><a class="Symbol" id="862">}</a> <a class="Symbol" id="864">{</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="865">A</a> <a class="Symbol" id="867">:</a> <a class="Primitive" href="Agda.Primitive.html#326" id="869">Set</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="873">a</a><a class="Symbol" id="874">}</a>  <a class="Comment" id="877">-- The underlying set</a>
  <a class="Symbol" id="901">(</a><a class="Bound Operator" href="Algebra.Lattice.Structures.Biased.html#902" id="902">_≈_</a> <a class="Symbol" id="906">:</a> <a class="Function" href="Relation.Binary.Core.html#882" id="908">Rel</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="912">A</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="914">ℓ</a><a class="Symbol" id="915">)</a>    <a class="Comment" id="920">-- The underlying equality relation</a>
  <a class="Keyword" id="958">where</a>

<a class="Keyword" id="965">open</a> <a class="Keyword" id="970">import</a> <a class="Module" href="Algebra.Definitions.html" id="977">Algebra.Definitions</a> <a class="Bound Operator" href="Algebra.Lattice.Structures.Biased.html#902" id="997">_≈_</a>
<a class="Keyword" id="1001">open</a> <a class="Keyword" id="1006">import</a> <a class="Module" href="Algebra.Lattice.Structures.html" id="1013">Algebra.Lattice.Structures</a> <a class="Bound Operator" href="Algebra.Lattice.Structures.Biased.html#902" id="1040">_≈_</a>

<a class="Keyword" id="1045">private</a>
  <a class="Keyword" id="1055">variable</a>
    <a class="Generalizable" href="Algebra.Lattice.Structures.Biased.html#1068" id="1068">∧</a> <a class="Generalizable" href="Algebra.Lattice.Structures.Biased.html#1070" id="1070">∨</a> <a class="Symbol" id="1072">:</a> <a class="Function" href="Algebra.Core.html#518" id="1074">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="1078">A</a>
    <a class="Generalizable" href="Algebra.Lattice.Structures.Biased.html#1084" id="1084">¬</a>   <a class="Symbol" id="1088">:</a> <a class="Function" href="Algebra.Core.html#475" id="1090">Op₁</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="1094">A</a>
    <a class="Generalizable" href="Algebra.Lattice.Structures.Biased.html#1100" id="1100">⊤</a> <a class="Generalizable" href="Algebra.Lattice.Structures.Biased.html#1102" id="1102">⊥</a> <a class="Symbol" id="1104">:</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="1106">A</a>

<a class="Comment" id="1109">------------------------------------------------------------------------</a>
<a class="Comment" id="1182">-- Lattice</a>

<a class="Comment" id="1194">-- An alternative form of `IsLattice` defined in terms of</a>
<a class="Comment" id="1252">-- `IsJoinSemilattice` and `IsMeetLattice`. This form may be desirable</a>
<a class="Comment" id="1323">-- to use when constructing a lattice object as it requires fewer</a>
<a class="Comment" id="1389">-- arguments, but is often a mistake to use as an argument as it</a>
<a class="Comment" id="1454">-- contains two, *potentially different*, proofs that the equality</a>
<a class="Comment" id="1521">-- relation _≈_ is an equivalence.</a>

<a class="Keyword" id="1557">record</a> <a id="IsLattice₂"></a><a class="Record" href="Algebra.Lattice.Structures.Biased.html#1564" id="1564">IsLattice₂</a> <a class="Symbol" id="1575">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1576" id="1576">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1578" id="1578">∧</a> <a class="Symbol" id="1580">:</a> <a class="Function" href="Algebra.Core.html#518" id="1582">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="1586">A</a><a class="Symbol" id="1587">)</a> <a class="Symbol" id="1589">:</a> <a class="Primitive" href="Agda.Primitive.html#326" id="1591">Set</a> <a class="Symbol" id="1595">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="1596">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="1598">⊔</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="1600">ℓ</a><a class="Symbol" id="1601">)</a> <a class="Keyword" id="1603">where</a>
  <a class="Keyword" id="1611">field</a>
    <a id="IsLattice₂.isJoinSemilattice"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#1621" id="1621">isJoinSemilattice</a> <a class="Symbol" id="1639">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#1637" id="1641">IsJoinSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1576" id="1659">∨</a>
    <a id="IsLattice₂.isMeetSemilattice"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#1665" id="1665">isMeetSemilattice</a> <a class="Symbol" id="1683">:</a> <a class="Function" href="Algebra.Lattice.Structures.html#1348" id="1685">IsMeetSemilattice</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1578" id="1703">∧</a>
    <a id="IsLattice₂.absorptive"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#1709" id="1709">absorptive</a>        <a class="Symbol" id="1727">:</a> <a class="Function" href="Algebra.Definitions.html#2938" id="1729">Absorptive</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1576" id="1740">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1578" id="1742">∧</a>

  <a class="Keyword" id="1747">module</a> <a id="IsLattice₂.ML"></a><a class="Module" href="Algebra.Lattice.Structures.Biased.html#1754" id="1754">ML</a> <a class="Symbol" id="1757">=</a> <a class="Module" href="Algebra.Lattice.Structures.html#1389" id="1759">IsMeetSemilattice</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#1665" id="1777">isMeetSemilattice</a>
  <a class="Keyword" id="1797">module</a> <a id="IsLattice₂.JL"></a><a class="Module" href="Algebra.Lattice.Structures.Biased.html#1804" id="1804">JL</a> <a class="Symbol" id="1807">=</a> <a class="Module" href="Algebra.Lattice.Structures.html#1678" id="1809">IsJoinSemilattice</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#1621" id="1827">isJoinSemilattice</a>

  <a id="IsLattice₂.isLattice₂"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#1848" id="1848">isLattice₂</a> <a class="Symbol" id="1859">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#4004" id="1861">IsLattice</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1576" id="1871">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#1578" id="1873">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.Biased.html#1848" id="1877">isLattice₂</a> <a class="Symbol" id="1888">=</a> <a class="Keyword" id="1890">record</a>
    <a class="Symbol" id="1901">{</a> <a class="Field" href="Algebra.Lattice.Structures.html#4060" id="1903">isEquivalence</a> <a class="Symbol" id="1917">=</a> <a class="Function" href="Algebra.Structures.html#1169" id="1919">ML.isEquivalence</a>
    <a class="Symbol" id="1940">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4098" id="1942">∨-comm</a>        <a class="Symbol" id="1956">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#1132" id="1958">JL.comm</a>
    <a class="Symbol" id="1970">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4132" id="1972">∨-assoc</a>       <a class="Symbol" id="1986">=</a> <a class="Function" href="Algebra.Structures.html#1864" id="1988">JL.assoc</a>
    <a class="Symbol" id="2001">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4166" id="2003">∨-cong</a>        <a class="Symbol" id="2017">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#1792" id="2019">JL.∨-cong</a>
    <a class="Symbol" id="2033">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4199" id="2035">∧-comm</a>        <a class="Symbol" id="2049">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#1132" id="2051">ML.comm</a>
    <a class="Symbol" id="2063">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4233" id="2065">∧-assoc</a>       <a class="Symbol" id="2079">=</a> <a class="Function" href="Algebra.Structures.html#1864" id="2081">ML.assoc</a>
    <a class="Symbol" id="2094">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4267" id="2096">∧-cong</a>        <a class="Symbol" id="2110">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#1503" id="2112">ML.∧-cong</a>
    <a class="Symbol" id="2126">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4300" id="2128">absorptive</a>    <a class="Symbol" id="2142">=</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#1709" id="2144">absorptive</a>
    <a class="Symbol" id="2159">}</a>

<a class="Keyword" id="2162">open</a> <a class="Module" href="Algebra.Lattice.Structures.Biased.html#1564" id="2167">IsLattice₂</a> <a class="Keyword" id="2178">public</a> <a class="Keyword" id="2185">using</a> <a class="Symbol" id="2191">(</a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#1848" id="2192">isLattice₂</a><a class="Symbol" id="2202">)</a>

<a class="Comment" id="2205">------------------------------------------------------------------------</a>
<a class="Comment" id="2278">-- DistributiveLattice</a>

<a class="Comment" id="2302">-- A version of distributive lattice that is biased towards the (r)ight</a>
<a class="Comment" id="2374">-- distributivity law for (j)oin and (m)eet.</a>
<a class="Keyword" id="2419">record</a> <a id="IsDistributiveLatticeʳʲᵐ"></a><a class="Record" href="Algebra.Lattice.Structures.Biased.html#2426" id="2426">IsDistributiveLatticeʳʲᵐ</a> <a class="Symbol" id="2451">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2452" id="2452">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2454" id="2454">∧</a> <a class="Symbol" id="2456">:</a> <a class="Function" href="Algebra.Core.html#518" id="2458">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="2462">A</a><a class="Symbol" id="2463">)</a> <a class="Symbol" id="2465">:</a> <a class="Primitive" href="Agda.Primitive.html#326" id="2467">Set</a> <a class="Symbol" id="2471">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="2472">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="2474">⊔</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="2476">ℓ</a><a class="Symbol" id="2477">)</a> <a class="Keyword" id="2479">where</a>
  <a class="Keyword" id="2487">field</a>
    <a id="IsDistributiveLatticeʳʲᵐ.isLattice"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#2497" id="2497">isLattice</a>    <a class="Symbol" id="2510">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#4004" id="2512">IsLattice</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2452" id="2522">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2454" id="2524">∧</a>
    <a id="IsDistributiveLatticeʳʲᵐ.∨-distribʳ-∧"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#2530" id="2530">∨-distribʳ-∧</a> <a class="Symbol" id="2543">:</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2452" id="2545">∨</a> <a class="Function Operator" href="Algebra.Definitions.html#2346" id="2547">DistributesOverʳ</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2454" id="2564">∧</a>

  <a class="Keyword" id="2569">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#4004" id="2574">IsLattice</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#2497" id="2584">isLattice</a> <a class="Keyword" id="2594">public</a>

  <a id="IsDistributiveLatticeʳʲᵐ.setoid"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#2604" id="2604">setoid</a> <a class="Symbol" id="2611">:</a> <a class="Record" href="Relation.Binary.Bundles.html#1009" id="2613">Setoid</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="2620">a</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="2622">ℓ</a>
  <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2604" id="2626">setoid</a> <a class="Symbol" id="2633">=</a> <a class="Keyword" id="2635">record</a> <a class="Symbol" id="2642">{</a> <a class="Field" href="Relation.Binary.Bundles.html#1132" id="2644">isEquivalence</a> <a class="Symbol" id="2658">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#4060" id="2660">isEquivalence</a> <a class="Symbol" id="2674">}</a>

  <a id="IsDistributiveLatticeʳʲᵐ.∨-distrib-∧"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#2679" id="2679">∨-distrib-∧</a>  <a class="Symbol" id="2692">=</a> <a class="Function" href="Algebra.Consequences.Setoid.html#6090" id="2694">comm+distrʳ⇒distr</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2604" id="2712">setoid</a> <a class="Function" href="Algebra.Lattice.Structures.html#4267" id="2719">∧-cong</a> <a class="Function" href="Algebra.Lattice.Structures.html#4098" id="2726">∨-comm</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#2530" id="2733">∨-distribʳ-∧</a>
  <a id="IsDistributiveLatticeʳʲᵐ.∧-distribˡ-∨"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#2748" id="2748">∧-distribˡ-∨</a> <a class="Symbol" id="2761">=</a> <a class="Function" href="Algebra.Consequences.Setoid.html#6669" id="2763">distrib+absorbs⇒distribˡ</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2604" id="2788">setoid</a> <a class="Function" href="Algebra.Lattice.Structures.html#4267" id="2795">∧-cong</a> <a class="Function" href="Algebra.Lattice.Structures.html#4233" id="2802">∧-assoc</a> <a class="Function" href="Algebra.Lattice.Structures.html#4098" id="2810">∨-comm</a> <a class="Function" href="Algebra.Lattice.Structures.html#4439" id="2817">∧-absorbs-∨</a> <a class="Function" href="Algebra.Lattice.Structures.html#4377" id="2829">∨-absorbs-∧</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2679" id="2841">∨-distrib-∧</a>
  <a id="IsDistributiveLatticeʳʲᵐ.∧-distrib-∨"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#2855" id="2855">∧-distrib-∨</a>  <a class="Symbol" id="2868">=</a> <a class="Function" href="Algebra.Consequences.Setoid.html#5951" id="2870">comm+distrˡ⇒distr</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2604" id="2888">setoid</a> <a class="Function" href="Algebra.Lattice.Structures.html#4166" id="2895">∨-cong</a> <a class="Function" href="Algebra.Lattice.Structures.html#4199" id="2902">∧-comm</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2748" id="2909">∧-distribˡ-∨</a>

  <a id="IsDistributiveLatticeʳʲᵐ.isDistributiveLatticeʳʲᵐ"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#2925" id="2925">isDistributiveLatticeʳʲᵐ</a> <a class="Symbol" id="2950">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#4753" id="2952">IsDistributiveLattice</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2452" id="2974">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#2454" id="2976">∧</a>
  <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2925" id="2980">isDistributiveLatticeʳʲᵐ</a> <a class="Symbol" id="3005">=</a> <a class="Keyword" id="3007">record</a>
    <a class="Symbol" id="3018">{</a> <a class="Field" href="Algebra.Lattice.Structures.html#4821" id="3020">isLattice</a>   <a class="Symbol" id="3032">=</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#2497" id="3034">isLattice</a>
    <a class="Symbol" id="3048">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4853" id="3050">∨-distrib-∧</a> <a class="Symbol" id="3062">=</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2679" id="3064">∨-distrib-∧</a>
    <a class="Symbol" id="3080">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#4891" id="3082">∧-distrib-∨</a> <a class="Symbol" id="3094">=</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#2855" id="3096">∧-distrib-∨</a>
    <a class="Symbol" id="3112">}</a>

<a class="Keyword" id="3115">open</a> <a class="Module" href="Algebra.Lattice.Structures.Biased.html#2426" id="3120">IsDistributiveLatticeʳʲᵐ</a> <a class="Keyword" id="3145">public</a> <a class="Keyword" id="3152">using</a> <a class="Symbol" id="3158">(</a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#2925" id="3159">isDistributiveLatticeʳʲᵐ</a><a class="Symbol" id="3183">)</a>

<a class="Comment" id="3186">------------------------------------------------------------------------</a>
<a class="Comment" id="3259">-- BooleanAlgebra</a>

<a class="Comment" id="3278">-- A (r)ight biased version of a boolean algebra.</a>
<a class="Keyword" id="3328">record</a> <a id="IsBooleanAlgebraʳ"></a><a class="Record" href="Algebra.Lattice.Structures.Biased.html#3335" id="3335">IsBooleanAlgebraʳ</a>
         <a class="Symbol" id="3362">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3363" id="3363">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3365" id="3365">∧</a> <a class="Symbol" id="3367">:</a> <a class="Function" href="Algebra.Core.html#518" id="3369">Op₂</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="3373">A</a><a class="Symbol" id="3374">)</a> <a class="Symbol" id="3376">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3377" id="3377">¬</a> <a class="Symbol" id="3379">:</a> <a class="Function" href="Algebra.Core.html#475" id="3381">Op₁</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="3385">A</a><a class="Symbol" id="3386">)</a> <a class="Symbol" id="3388">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3389" id="3389">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3391" id="3391">⊥</a> <a class="Symbol" id="3393">:</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#865" id="3395">A</a><a class="Symbol" id="3396">)</a> <a class="Symbol" id="3398">:</a> <a class="Primitive" href="Agda.Primitive.html#326" id="3400">Set</a> <a class="Symbol" id="3404">(</a><a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="3405">a</a> <a class="Primitive Operator" href="Agda.Primitive.html#810" id="3407">⊔</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="3409">ℓ</a><a class="Symbol" id="3410">)</a> <a class="Keyword" id="3412">where</a>
  <a class="Keyword" id="3420">field</a>
    <a id="IsBooleanAlgebraʳ.isDistributiveLattice"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#3430" id="3430">isDistributiveLattice</a> <a class="Symbol" id="3452">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#4753" id="3454">IsDistributiveLattice</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3363" id="3476">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3365" id="3478">∧</a>
    <a id="IsBooleanAlgebraʳ.∨-complementʳ"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#3484" id="3484">∨-complementʳ</a>         <a class="Symbol" id="3506">:</a> <a class="Function" href="Algebra.Definitions.html#1792" id="3508">RightInverse</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3389" id="3521">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3377" id="3523">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3363" id="3525">∨</a>
    <a id="IsBooleanAlgebraʳ.∧-complementʳ"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#3531" id="3531">∧-complementʳ</a>         <a class="Symbol" id="3553">:</a> <a class="Function" href="Algebra.Definitions.html#1792" id="3555">RightInverse</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3391" id="3568">⊥</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3377" id="3570">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3365" id="3572">∧</a>
    <a id="IsBooleanAlgebraʳ.¬-cong"></a><a class="Field" href="Algebra.Lattice.Structures.Biased.html#3578" id="3578">¬-cong</a>                <a class="Symbol" id="3600">:</a> <a class="Function" href="Algebra.Definitions.html#797" id="3602">Congruent₁</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3377" id="3613">¬</a>

  <a class="Keyword" id="3618">open</a> <a class="Module" href="Algebra.Lattice.Structures.html#4753" id="3623">IsDistributiveLattice</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#3430" id="3645">isDistributiveLattice</a> <a class="Keyword" id="3667">public</a>

  <a id="IsBooleanAlgebraʳ.setoid"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#3677" id="3677">setoid</a> <a class="Symbol" id="3684">:</a> <a class="Record" href="Relation.Binary.Bundles.html#1009" id="3686">Setoid</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#859" id="3693">a</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#861" id="3695">ℓ</a>
  <a class="Function" href="Algebra.Lattice.Structures.Biased.html#3677" id="3699">setoid</a> <a class="Symbol" id="3706">=</a> <a class="Keyword" id="3708">record</a> <a class="Symbol" id="3715">{</a> <a class="Field" href="Relation.Binary.Bundles.html#1132" id="3717">isEquivalence</a> <a class="Symbol" id="3731">=</a> <a class="Function" href="Algebra.Lattice.Structures.html#4060" id="3733">isEquivalence</a> <a class="Symbol" id="3747">}</a>

  <a id="IsBooleanAlgebraʳ.isBooleanAlgebraʳ"></a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#3752" id="3752">isBooleanAlgebraʳ</a> <a class="Symbol" id="3770">:</a> <a class="Record" href="Algebra.Lattice.Structures.html#5398" id="3772">IsBooleanAlgebra</a>  <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3363" id="3790">∨</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3365" id="3792">∧</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3377" id="3794">¬</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3389" id="3796">⊤</a> <a class="Bound" href="Algebra.Lattice.Structures.Biased.html#3391" id="3798">⊥</a>
  <a class="Function" href="Algebra.Lattice.Structures.Biased.html#3752" id="3802">isBooleanAlgebraʳ</a> <a class="Symbol" id="3820">=</a> <a class="Keyword" id="3822">record</a>
    <a class="Symbol" id="3833">{</a> <a class="Field" href="Algebra.Lattice.Structures.html#5486" id="3835">isDistributiveLattice</a> <a class="Symbol" id="3857">=</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#3430" id="3859">isDistributiveLattice</a>
    <a class="Symbol" id="3885">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#5540" id="3887">∨-complement</a>          <a class="Symbol" id="3909">=</a> <a class="Function" href="Algebra.Consequences.Setoid.html#4010" id="3911">comm+invʳ⇒inv</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#3677" id="3925">setoid</a> <a class="Function" href="Algebra.Lattice.Structures.html#4098" id="3932">∨-comm</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#3484" id="3939">∨-complementʳ</a>
    <a class="Symbol" id="3957">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#5582" id="3959">∧-complement</a>          <a class="Symbol" id="3981">=</a> <a class="Function" href="Algebra.Consequences.Setoid.html#4010" id="3983">comm+invʳ⇒inv</a> <a class="Function" href="Algebra.Lattice.Structures.Biased.html#3677" id="3997">setoid</a> <a class="Function" href="Algebra.Lattice.Structures.html#4199" id="4004">∧-comm</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#3531" id="4011">∧-complementʳ</a>
    <a class="Symbol" id="4029">;</a> <a class="Field" href="Algebra.Lattice.Structures.html#5624" id="4031">¬-cong</a>                <a class="Symbol" id="4053">=</a> <a class="Field" href="Algebra.Lattice.Structures.Biased.html#3578" id="4055">¬-cong</a>
    <a class="Symbol" id="4066">}</a>

<a class="Keyword" id="4069">open</a> <a class="Module" href="Algebra.Lattice.Structures.Biased.html#3335" id="4074">IsBooleanAlgebraʳ</a> <a class="Keyword" id="4092">public</a> <a class="Keyword" id="4099">using</a> <a class="Symbol" id="4105">(</a><a class="Function" href="Algebra.Lattice.Structures.Biased.html#3752" id="4106">isBooleanAlgebraʳ</a><a class="Symbol" id="4123">)</a>
</pre></div></body></html>